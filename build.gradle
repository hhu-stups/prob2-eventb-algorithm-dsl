import java.util.concurrent.TimeUnit

plugins {
	id "java-library"
	id "groovy"
}

group = "de.hhu.stups"
version = "3.2.12-SNAPSHOT"
final isSnapshot = project.version.endsWith("-SNAPSHOT")

wrapper {
	gradleVersion = "6.8.2"
	distributionType = Wrapper.DistributionType.ALL
}

repositories {
	mavenCentral()
	if (isSnapshot) {
		maven {
			name "Sonatype snapshots"
			url "https://oss.sonatype.org/content/repositories/snapshots"
		}
	}
}

configurations.all {
	resolutionStrategy {
		cacheChangingModulesFor(0, TimeUnit.SECONDS)
	}
}

sourceSets {
	main {
		// Compile all sources using the Groovy compiler, to make Groovy classes visible to Java code
		groovy {
			srcDirs += java.srcDirs
		}
		java {
			srcDirs = []
		}
	}
}

dependencies {
	implementation group: "com.google.code.gson", name: "gson", version: "2.8.6" // Apache License 2.0
	api group: "de.hhu.stups", name: "de.prob2.kernel", version: "3.14.0" // Eclipse Public License 2.0
	implementation group: "de.hhu.stups", name: "eventbalg", version: "2.9.27" // Eclipse Public License 1.0

	// Test dependencies
	testImplementation group: "org.spockframework", name: "spock-core", version: "2.0-M4-groovy-3.0" // Apache License 2.0
	testRuntimeOnly group: "de.hhu.stups", name: "de.prob2.commandline", version: "3.14.0"
	// Byte Buddy and Objenesis are needed by Spock to mock classes
	testRuntimeOnly group: "net.bytebuddy", name: "byte-buddy", version: "1.10.20" // Apache License 2.0
	testRuntimeOnly group: "org.objenesis", name: "objenesis", version: "3.1" // Apache License 2.0
}

java {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
}

final SOURCE_ENCODING = "UTF-8"

tasks.withType(JavaCompile) {
	options.encoding = SOURCE_ENCODING
}

tasks.withType(GroovyCompile) {
	options.encoding = SOURCE_ENCODING
}

tasks.withType(Javadoc) {
	options.encoding = SOURCE_ENCODING
}

// The Groovydoc encoding can't be set via the task, it depends on the file.encoding system property of the Gradle JVM, which is set in the gradle.properties file.

task groovyIntegrationTests(type: JavaExec, dependsOn: [classes, test]) {
	main = "de.prob2.commandline.CommandLineMain"
	classpath = sourceSets.test.runtimeClasspath
	args = ["-script", "groovyTests"]
}
check.dependsOn(groovyIntegrationTests)
