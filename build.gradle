import java.util.concurrent.TimeUnit

plugins {
	id "java-library"
	id "groovy"
	id "de.hhu.stups.sablecc" version "1.2.0"
}

group = "de.hhu.stups"
version = "3.2.12-SNAPSHOT"
final isSnapshot = project.version.endsWith("-SNAPSHOT")

final snapshotsRepoUrl = "https://central.sonatype.com/repository/maven-snapshots/"
repositories {
	mavenCentral()
	if (isSnapshot) {
		maven {
			name = "Sonatype snapshots"
			url = snapshotsRepoUrl
		}
	}
}

configurations.all {
	resolutionStrategy {
		cacheChangingModulesFor(0, TimeUnit.SECONDS)
	}
}

sourceSets {
	main {
		// Compile all sources using the Groovy compiler, to make Groovy classes visible to Java code
		groovy {
			srcDirs += java.srcDirs
		}
		java {
			srcDirs = []
		}
	}
}

dependencies {
	final sableCCVersion = "3.9.0"
	sableCC("de.hhu.stups:sablecc:${sableCCVersion}")
	api("de.hhu.stups:sablecc-runtime:${sableCCVersion}")
	implementation("com.github.krukow:clj-ds:0.0.4") // Eclipse Public License 1.0
	implementation("com.google.code.gson:gson:2.10.1") // Apache License 2.0
	api(platform("de.hhu.stups:prob-java-bom:4.15.0"))
	api("de.hhu.stups:de.prob2.kernel") // Eclipse Public License 2.0

	// Test dependencies
	// The JUnit version is set by junit-bom pulled in by spock-core.
	testRuntimeOnly("org.junit.platform:junit-platform-launcher")
	testImplementation("org.junit.vintage:junit-vintage-engine")
	testImplementation("org.spockframework:spock-core:2.3-groovy-4.0") // Apache License 2.0
	testRuntimeOnly("de.hhu.stups:de.prob2.commandline")
}

java {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
}

final SOURCE_ENCODING = "UTF-8"

tasks.withType(JavaCompile) {
	options.encoding = SOURCE_ENCODING
}

tasks.withType(GroovyCompile) {
	options.encoding = SOURCE_ENCODING
}

tasks.withType(Javadoc) {
	options.encoding = SOURCE_ENCODING
}

// The Groovydoc encoding can't be set via the task, it depends on the file.encoding system property of the Gradle JVM, which is set in the gradle.properties file.

test {
	useJUnitPlatform()
}

task groovyIntegrationTests(type: JavaExec, dependsOn: [classes, test]) {
	mainClass = "de.prob2.commandline.CommandLineMain"
	classpath = sourceSets.test.runtimeClasspath
	args = ["-script", "groovyTests"]
}
check.dependsOn(groovyIntegrationTests)

// Run a single groovy Testcase
// Usage: ./gradlew groovyTest --args="-script groovyTests/theoryIntegration.groovy"
task groovyTest(type: JavaExec) {
	mainClass = "de.prob2.commandline.CommandLineMain"
	classpath = sourceSets.test.runtimeClasspath
}
